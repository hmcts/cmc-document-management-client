buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
  }
}

plugins {
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.0.3.RELEASE'
  id 'org.springframework.boot' version '1.5.8.RELEASE'
}

apply plugin: 'checkstyle'
apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'uk.gov.hmcts.reform'
version '1.0.1'

checkstyle {
  maxWarnings = 0
  toolVersion = '8.3'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

sourceCompatibility = 1.8

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation'
}

repositories {
  maven {
    url  "https://dl.bintray.com/hmcts/hmcts-maven"
  }
  jcenter()
  mavenLocal()
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}


def pomConfig = {
  licenses {
    license {
      name "MIT License"
      url "http://www.opensource.org/licenses/mit-license.php"
      distribution "repo"
    }
  }

  scm {
    url "https://github.com/hmcts/document-management-client"
  }
}


task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'document-management-client'
      version project.version

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Client library for communicating with document management gateway')
        root.appendNode('name', 'Document management client')
        root.appendNode('url', 'https://github.com/hmcts/document-management-client')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['Main']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = 'document-management-client'
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/hmcts/document-management-client'
    version {
      name = project.version
    }
  }
}

ext {
  springCloudVersion = 'Dalston.SR4'
}

dependencies {
  compile('org.springframework.cloud:spring-cloud-starter-feign')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.testcontainers:testcontainers:1.4.3')
  testCompile('com.github.tomakehurst:wiremock:2.10.1')
  testCompile('org.eclipse.jetty:jetty-server')
  testCompile('org.eclipse.jetty:jetty-servlet')
  testCompile('org.eclipse.jetty:jetty-servlets')
  testCompile('org.apache.httpcomponents:httpclient')
  compile group: 'commons-io', name: 'commons-io', version: '2.5'
  compile('com.netflix.feign:feign-jackson:8.18.0')
  compile('io.github.openfeign.form:feign-form-spring:2.0.5')
  testCompile('org.assertj:assertj-core:3.8.0')

}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

bootRepackage {
  enabled = false;
}
